program         -> declaration* EOF ;

# Declarations ===============================================================

declaration     -> varDecl
                | constDecl
                | classDecl
                | funDecl
                | statement ;

varDecl         -> "let" IDENTIFIER ( "," IDENTIFIER )* ( "=" expression )? ";" ;
constDecl       -> "const" IDENTIFIER ( "," IDENTIFIER )* "=" expression ";" ;
classDecl       -> "class" IDENTIFIER "{" ("func" function)* "}" ;

funDecl         -> "func" function ;
function        -> IDENTIFIER "(" parameters? ")" block ;

parameters      -> DENTIFIER ( "," IDENTIFIER )* ;

# Satatements ================================================================

statement       -> exprStmt
                | ifStmt
                | whileStmt
                | forStmt
                | breakStmt
                | contiueStmt
                | returnStmt
                | importStmt
                | block ;

whileStmt       -> "while" "(" expression ")" statement ;
forStmt         -> "for" "(" ( varDecl | exprStmt | ";" ) expression? ";" expression? ")" statement ;

ifStmt          -> "if" "(" expression ")" statement ( "else" statement )? ;

breakStmt       -> "break" ";"? ;
continueStmt    -> "continue" ";"? ;
returnStmt      -> "return" expression? ";"? ;

block           -> "{" declaration* "}" ;

importStmt      -> "import" STRING ";"? ;

exprStmt        -> expression ";"? ;


# Expressions ================================================================

expression      -> assignment ;
assignment      -> IDENTIFIER "=" assignment
                | logic_or ;
logic_or        -> logic_and ("||" logic_and)* ;
logic_and       -> equality ("&&" equality)* ;
equality        -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison      -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term            -> factor ( ( "-" | "+" ) factor )* ;
factor          -> expo ( ( "/" | "*" | "%" ) expo )* ;
expo            -> unary ("**" unary)* ;
unary           -> ( "!" | "-" | "+" ) unary
                | indexing
                | lambda
                | instance
                | call ;

call            -> primary ( "(" arguments? ")" )* ;
indexing        -> primary ( "[" expression "]" )* ;
instance        -> "new" primary "(" arguments? ")" ;
lambda          -> "func" "(" parameters? ")" block ;

primary         -> INTEGER | REAL | STRING
                | "true" | "false" | "null"
                | "(" expression ")"
                | array
                | IDENTIFIER ;

array           -> "[" (expression ("," expression)*)? "]" ;

arguments       -> expression ( "," expression )* ;
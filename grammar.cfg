program         -> declaration* EOF ;

# Declarations ===============================================================

declaration     -> varDecl
                | constDecl
                | statement ;

varDecl         -> "let" IDENTIFIER ( "," IDENTIFIER )* ( "=" expression )? ";" ;
constDecl       -> "const" IDENTIFIER ( "," IDENTIFIER )* "=" expression ";" ;

# Satatements ================================================================

statement       -> exprStmt
                | ifStmt
                | printStmt
                | whileStmt
                | forStmt
                | block ;

whileStmt       -> "while" "(" expression ")" statement ;
forStmt         -> "for" "(" ( varDecl | exprStmt | ";" ) expression? ";" expression? ")" statement ;

ifStmt          -> "if" "(" expression ")" statement ( "else" statement )? ;

block           -> "{" declaration* "}" ;

exprStmt        -> expression ";" ;
printStmt       -> "print" "(" expression ")" ";"? ;


# Expressions ================================================================

expression      -> assignment ;
assignment      -> IDENTIFIER "=" assignment
                | logic_or ;
logic_or        -> logic_and ("||" logic_and)* ;
logic_and       -> equality ("&&" equality)* ;
equality        -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison      -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term            -> factor ( ( "-" | "+" ) factor )* ;
factor          -> expo ( ( "/" | "*" | "%" ) expo )* ;
expo            -> unary ("**" unary)* ;
unary           -> ( "!" | "-" | "+" ) unary | primary ;

primary         -> INTEGER | REAL | STRING
                | "true" | "false" | "null"
                | "(" expression ")" ;
                | IDENTIFIER ;